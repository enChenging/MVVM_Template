apply plugin: 'com.android.application'
apply plugin: 'androidx.navigation.safeargs'
apply plugin: 'org.greenrobot.greendao'

android {

    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        applicationId rootProject.ext.android.applicationId
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName

        flavorDimensions "versionCode"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
//                includeCompileClasspath true
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        debug {
            keyAlias 'android'
            keyPassword 'android'
            storeFile file('mr_release.jks')
            storePassword 'android'
        }
        release {
            keyAlias 'android'
            keyPassword 'android'
            storeFile file('mr_release.jks')
            storePassword 'android'
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            buildConfigField "boolean", "LOG_DEBUG", "false"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.release
            buildConfigField "boolean", "LOG_DEBUG", "true"
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
            if (isModule.toBoolean()) {
                manifest.srcFile 'src/module/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                resources {
                    exclude 'src/debug/*'
                }
            }
        }
    }

    dataBinding {
        enabled true
    }


    productFlavors {
        mvvm {
            applicationId "com.release.mvvm"
            resValue "string", "name", "mvvm"
            manifestPlaceholders = [app_icon     : "@mipmap/ic_launcher2",
                                    app_roundicon: "@mipmap/ic_launcher2"]
        }
    }

    android.applicationVariants.all {
        variant ->
            variant.outputs.all {
                outputFileName = "${productFlavors[0].name}_v${defaultConfig.versionName}_${buildType.name}_${releaseTime()}.apk"
            }
    }
}

static def releaseTime() {
    return new Date().format("yyMMdd", TimeZone.getTimeZone("UTC"))
}

greendao {
    schemaVersion 1 //数据库版本号
//            targetGenDir 'src/main/java'//设置DaoMaster、DaoSession、Dao目录
//            generateTests false //设置为true以自动生成单元测试。
//            targetGenDirTests 'src/main/java' //应存储生成的单元测试的基本目录。默认为 src / androidTest / java。
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    annotationProcessor rootProject.ext.dependencies["dagger-android-processor"]
    annotationProcessor rootProject.ext.dependencies["dagger-compiler"]
    annotationProcessor rootProject.ext.dependencies["arouter-compiler"]

    if (isModule.toBoolean()) {
        implementation project(':lib_base')
    } else {
        implementation project(':module_login')
    }

}
